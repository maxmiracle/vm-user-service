plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.1'
	id 'io.spring.dependency-management' version '1.1.7'
    id 'org.openapi.generator' version '7.14.0'
    id 'org.jooq.jooq-codegen-gradle' version '3.19.17'
}

group = 'ru.maximserver'
version = '0.0.1-SNAPSHOT'
description = 'User information service'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

sourceSets {
    main {
        java {
            srcDirs += ["$project.buildDir/generated/openapi/src/main/java"]
        }
    }
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	implementation 'org.springframework.boot:spring-boot-starter-jooq'
	implementation 'org.springframework.boot:spring-boot-starter-liquibase'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework:spring-jdbc'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
	runtimeOnly 'org.postgresql:postgresql'
	runtimeOnly 'org.postgresql:r2dbc-postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-actuator-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-data-r2dbc-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-jooq-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-liquibase-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-webflux-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.testcontainers:testcontainers-junit-jupiter'
	testImplementation 'org.testcontainers:testcontainers-postgresql'
	testImplementation 'org.testcontainers:testcontainers-r2dbc'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'jakarta.validation:jakarta.validation-api'
    //implementation 'org.openapitools:openapi-generator-gradle-plugin:7.14.0' exclude(group: 'org.slf4j', module: 'slf4j-simple')
    implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:3.0.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webflux-api:3.0.0'
    jooqCodegen "org.jooq:jooq-meta-extensions-liquibase:3.19.17"
}

tasks.named('test') {
	useJUnitPlatform()
}

compileJava.dependsOn "openApiGenerate"
openApiGenerate {
    Directory outputGenerated = layout.buildDirectory.dir("generated").get()
    generatorName.set("spring")
    inputSpec.set("$rootDir/src/main/resources/specification/openapi-users.yaml")
    outputDir.set("$outputGenerated/openapi")
    apiPackage.set("ru.maximserver.vmuserservice.api")
    modelPackage.set("ru.maximserver.vmuserservice.model")
    configOptions.set([library                             : "spring-boot",
                       useOptional                         : "true",
                       openApiNullable                     : "false",
                       interfaceOnly                       : "true",
                       generatedConstructorWithRequiredArgs: "false",
                       useTags                             : "true",
                       basePackage                         : "ru.maximserver.vmuserservice",
                       useJakartaEe                        : "true",
                       reactive                            : "true"])
}

compileJava.dependsOn "jooqCodegen"
jooq {
    configuration {
        generator {
            database {
                name = "org.jooq.meta.extensions.liquibase.LiquibaseDatabase"
                properties {
                    property {
                        key = "rootPath"
                        value = "$rootDir/src/main/resources"
                    }
                    property {
                        key = "scripts"
                        value = "/db/changelog/db.changelog-master.yaml"
                    }
                    property {
                        key = "includeLiquibaseTables"
                        value = false
                    }
                    property {
                        key = "liquibaseSchemaName"
                        value = "public"
                    }
                    property {
                        key = "changeLogParameters.contexts"
                        value = "!test"
                    }
                }
            }
            target {
                packageName = "ru.maximserver.vmuserservice.jooq.gen"
            }
        }
    }
}
